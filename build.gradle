plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name

String compatible_minecraft_versions =
		(project.min_minecraft_version != project.minecraft_version ? project.min_minecraft_version + "-" : "") +
		project.minecraft_version

version = project.mod_version + "+MC" + compatible_minecraft_versions
group = project.maven_group

repositories {
	maven {
		name = "CottonMC"
		url = "http://server.bbkr.space:8081/artifactory/libs-release"
	}

	maven {
		name = "Ladysnake Libs"
		url = "https://dl.bintray.com/ladysnake/libs"
	}

	maven {
		name = "Spinnery"
		url  "https://dl.bintray.com/spinnery/Spinnery"
	}

	maven {
		name = "BuildCraft"
		url = "https://mod-buildcraft.com/maven"
	}

	maven {
		name = "guntram"
		url = "https://minecraft.guntram.de/maven/"
	}

	// fallback/catch-all repos
	maven {
		name = "CurseMaven"
		url = "https://www.cursemaven.com"
	}

	// JITPACK MUST BE LAST
	maven {
		name = "JitPack"
		url 'https://jitpack.io'
	}

	jcenter()
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:" + project.minecraft_version
	mappings "net.fabricmc:yarn:" + project.yarn_mappings
	modImplementation "net.fabricmc:fabric-loader:" + project.loader_version

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:" + project.fabric_api_version
	// GUI library
	modImplementation ("io.github.cottonmc:LibGui:" + project.libgui_version) { exclude module: 'fabric-api' }
	include "io.github.cottonmc:LibGui:" + project.libgui_version
	// Config library
	modApi ("me.sargunvohra.mcmods:autoconfig1u:" + project.autoconfig1u_version) { exclude module: 'fabric-api' }
	include "me.sargunvohra.mcmods:autoconfig1u:" + project.autoconfig1u_version
	// Config GUI support
	modApi ("me.shedaniel.cloth:config-2:" + project.cloth_config_2_version) { exclude module: 'fabric-api' }
	include "me.shedaniel.cloth:config-2:" + project.cloth_config_2_version
	// Config menu support
	modImplementation ("io.github.prospector:modmenu:" + project.modmenu_version) { exclude module: 'fabric-api' }
	// translation fetching
	modImplementation "de.guntram.mcmod:crowdin-translate:" + project.crowdin_translate_version
	include "de.guntram.mcmod:crowdin-translate:" + project.crowdin_translate_version


	//DEV TEST
//	compile "curse.maven:slotlink:3029408"
//	modRuntime 'com.github.badasintended:slotlink:slotlink-1.1.4'
//	modCompile "alexiil.mc.lib:libblockattributes-all:0.8.1"
//	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.5.0"
//	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-chunk:2.5.0"
//	modRuntime "curse.maven:flamin_hot:3041192"
//	modRuntime "com.github.DashEightMate:flamin-hot:beta-0.0.2"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

processResources {
	inputs.property "version", project.version
	inputs.property "mod_id", project.mod_id

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand (
				"minecraft_version": project.minecraft_version,
				"min_minecraft_version": project.min_minecraft_version,
				"compatible_minecraft_versions": compatible_minecraft_versions,
				"loader_version": project.loader_version,

				"mod_id": project.mod_id,
				"init_class": project.ModId + "Init",
				"client_init_class": project.ModId + "ClientInit",
				"mod_version": project.version,
				"maven_group": project.maven_group,
				"archives_base_name": project.archives_base_name,
				"author": project.author,

				"fabric_api_version": project.fabric_api_version,
				"min_fabric_api_version": project.min_fabric_api_version
		)
	}


	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
